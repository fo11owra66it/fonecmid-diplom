
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьАкты(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбран месяц расчета.", , "Объект.Период");
		Возврат;
	КонецЕсли;
	
	// 1. Запуск фонового задания на сервере.
	ДлительнаяОперация = ЗапуститьДлительнуюОперацию();

	// 2. Подключение обработчика завершения фонового задания.
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	Оповещение = Новый ОписаниеОповещения("СформироватьАктыЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗапуститьДлительнуюОперацию()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"Обработки.ВКМ_МассовоеСозданиеАктов.СформироватьАктыНаСервере", Объект.Период);
	
КонецФункции

&НаКлиенте
Процедура СформироватьАктыЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда // Пользователь отменил задание.
		Возврат;
	КонецЕсли;

	//Если Результат.Статус = "Ошибка" Тогда
		//ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	//КонецЕсли;

	МассивДоговоров = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ЗаполнитьТабличнуюЧасть(МассивДоговоров);
	ОбщегоНазначенияКлиент.СообщитьПользователю("Обработка завершена");

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧасть(МассивДоговоров)
	
	ТабЧасть = Объект.СписокДоговоров;
	ТабЧасть.Очистить();
	
	Если МассивДоговоров <> Неопределено Тогда
		Для Каждого Элемент Из МассивДоговоров Цикл
			НоваяСтрока = ТабЧасть.Добавить();
			НоваяСтрока.Договор = Элемент.Договор;
			НоваяСтрока.Реализация = Элемент.Реализация;
		КонецЦикла; 
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

